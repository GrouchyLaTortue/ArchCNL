PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX conformance: <http://arch-ont.org/ontologies/architectureconformance#>
PREFIX famix: <http://arch-ont.org/ontologies/famix.owl#>
PREFIX architecture: <http://www.arch-ont.org/ontologies/architecture.owl#>

# Returns all statements which could be asserted for the rule 'Every Aggregate must residein a DomainRing.'
# An architecture rule consists of multiple statements. When a violation is detected, some statements could be
# asserted (i.e. the code ontologies complies with them/they are "true") while other statements could not be inferred
# (i.e. the code ontologies does not satisfy them/they are "false"). Both asserted and not-inferred statements
# provide information why and by which elements a rule is violated.

SELECT ?cnl ?subject ?predicate ?object
WHERE {
    GRAPH ?g {
        ?rule conformance:hasRuleRepresentation "Every Aggregate must residein a DomainRing."^^xsd:string.
        ?rule conformance:hasRuleRepresentation ?cnl.
        ?violation conformance:violates ?rule.
        ?proof conformance:proofs ?violation.
        ?proof conformance:hasAssertedStatement ?asserted. 
        ?asserted conformance:hasSubject ?subject.
        ?asserted conformance:hasPredicate ?predicate.
        ?asserted conformance:hasObject ?object.
    }
}
